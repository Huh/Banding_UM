  model{

    # Priors and constraints
    for (i in 1:nind){
       for (t in f[i]:(n.occasions-1)){
          logit(phi[i,t]) <- mean.phi
          logit(p[i,t]) <- mean.p
          } #t
       } #i

    mean.phi ~ dnorm(0, 0.34)         # Prior for mean survival
    mean.p ~ dnorm(0, 0.34)           # Prior for mean recapture
    survival <- 1/(1 + exp(-mean.phi))
    detection <- 1/(1 + exp(-mean.p))

    # Likelihood 
    for (i in 1:nind){
       # Define latent state at first capture
       z[i,f[i]] <- 1
       for (t in (f[i]+1):n.occasions){
          # State process
          z[i,t] ~ dbern(mu1[i,t])
          mu1[i,t] <- phi[i,t-1] * z[i,t-1]
          # Observation process
          y[i,t] ~ dbern(mu2[i,t])
          mu2[i,t] <- p[i,t-1] * z[i,t]
          } #t
       } #i
  }

